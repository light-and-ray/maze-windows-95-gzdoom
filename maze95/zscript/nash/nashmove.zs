//
// Source: https://forum.zdoom.org/viewtopic.php?t=35761
//
//===========================================================================
//
// nashmove.pk3
//
// Less slippery player movement. Works for any player class.
//
// Written by Nash Muhandes
//
// Feel free to use this in your mods. You don't have to ask my permission!
//
//===========================================================================



class Z_NashMove : CustomInventory
{
    Default
    {
        Inventory.MaxAmount 1;
        +INVENTORY.UNDROPPABLE
        +INVENTORY.UNTOSSABLE
        +INVENTORY.AUTOACTIVATE
    }

    // How much to reduce the slippery movement.
    // Lower number = less slippery.
    double DECEL_MULT;

    //===========================================================================
    //
    //
    //
    //===========================================================================

    bool bIsOnFloor(void)
    {
        return (Owner.Pos.Z == Owner.FloorZ) || (Owner.bOnMObj);
    }

    bool bIsInPain(void)
    {
        State PainState = Owner.FindState('Pain');
        if (PainState != NULL && Owner.InStateSequence(Owner.CurState, PainState))
        {
            return true;
        }
        return false;
    }

    double GetVelocity (void)
    {
        return Owner.Vel.Length();
    }

    //===========================================================================
    //
    //
    //
    //===========================================================================

    override void Tick(void)
    {
        if (Owner && Owner is "PlayerPawn")
        {
            if (speed == 0) {
                speed = Owner.speed;
                DECEL_MULT = 0.9 * speed;
            }
            if (bIsOnFloor())
            {
                // bump up the player's speed to compensate for the deceleration
                // TO DO: math here is shit and wrong, please fix
                double s = speed + (speed - DECEL_MULT);
                Owner.A_SetSpeed(s * 2);

                // decelerate the player, if not in pain
                if (!bIsInPain())
                {
                    Owner.vel.x *= DECEL_MULT;
                    Owner.vel.y *= DECEL_MULT;
                }

                // // make the view bobbing match the player's movement
                // PlayerPawn(Owner).ViewBob = DECEL_MULT;
            }
            else
            {
                Owner.A_SetSpeed(speed*2);
            }
        }

        Super.Tick();
    }

    //===========================================================================
    //
    //
    //
    //===========================================================================
    States
    {
    Use:
        TNT1 A 0;
        Fail;
    Pickup:
        TNT1 A 0
        {
            return true;
        }
        Stop;
    }
}
